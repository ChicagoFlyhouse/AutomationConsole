<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.3">
  <POU Name="FB_CoE_ReadParamArray" Id="{d455a0a2-2108-4cb5-abea-2d7a029601b5}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_CoE_ReadParamArray
VAR_INPUT
	bExecute: BOOL;
	sNetId: T_AmsNetID;
	nSlaveAddr: UINT;
	tTimeout: TIME;
	nSizeOfArr: USINT;
END_VAR
VAR_OUTPUT
	bBusy: BOOL;
	bDone: BOOL;
	/// Current ST_CoEDataArray Array Location being Read
	nCurArrLoc: INT;
	bError: BOOL;
	/// http://infosys.beckhoff.com/content/1033/tcadscommon/html/ads_returncodes.htm
	nErrorID: UDINT;
END_VAR
VAR
	fbCoE_Read: FB_EcCoESdoRead;
	nCase: INT;
	nIndex: INT;
END_VAR
VAR_IN_OUT
	stCoEDataArray: ST_CoEDataArray;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*++++++++++++++ CoE communication ++++++++++++++++++++++*)
fbCoE_Read.sNetId		:= sNetId;
fbCoE_Read.nSlaveAddr 	:= nSlaveAddr;
fbCoE_Read.tTimeout		:= tTimeout;

//Automatically Resets FB if bExecute is False
IF NOT bExecute THEN
	nCase 		:= 0;
	bError 		:= FALSE;
	bBusy 		:= FALSE;
	bDone 		:= FALSE;	
END_IF

CASE nCase OF
	0:
		IF bExecute THEN
			bBusy		:= TRUE;
			nCase 		:= 10;
			nErrorID 	:= 0;
			nCurArrLoc 	:= 1;
			fbCoE_Read(bExecute:=FALSE);
		END_IF
		
		
	10: //Read CoE Address Data
		fbCoE_Read(
			nIndex 		:= stCoEDataArray.stCoEData[nCurArrLoc].nIndex,
			nSubIndex 	:= stCoEDataArray.stCoEData[nCurArrLoc].nSubIndex,
			pDstBuf 	:= ADR(stCoEDataArray.stCoEData[nCurArrLoc].dwData),
			cbBufLen 	:= stCoEDataArray.stCoEData[nCurArrLoc].nDataLen,
			bExecute 	:= TRUE );
			
		IF fbCoE_Read.bError THEN
			nCase	:= 9999;
		ELSIF NOT fbCoE_Read.bBusy THEN
			nCase := 20;
		END_IF
	
		
	20: //Check for End of Array
		IF nCurArrLoc >= nSizeOfArr THEN
			nCase := 10000;
			bDone := TRUE;
		ELSE
			nCurArrLoc := nCurArrLoc + 1;
			nCase := 10;
			fbCoE_Read(bExecute:=FALSE);
		END_IF
		
			
	9999: //Error
		bError 	:= TRUE;
		nErrorID := fbCoE_Read.nErrID;
		fbCoE_Read(bExecute:=FALSE);
		nCase	:= 10000;
		
		
	10000: //Wait for Next 'Execute' Rising Edge
		bBusy := FALSE;
		IF NOT bExecute THEN
			nCase := 0;
		END_IF
		
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="FB_CoE_ReadParamArray">
      <LineId Id="3" Count="62" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>